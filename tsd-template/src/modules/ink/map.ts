export default `// ---- map ----
# title: NPC map
# author: Ed Knutson
// -----------------------------

VAR talked_to_hero_guard = false
VAR ring_taken = false
VAR helm_taken = false
VAR sword_taken = false
VAR hammer_taken = false
VAR flute_taken = false
VAR talked_to_fisher = false
VAR talked_to_miner = false
VAR talked_to_bard = false
VAR visited_tower = false
VAR visited_camphero = false
VAR visited_mountains = false
VAR visited_forest = false
VAR visited_town = false
VAR visited_lake = false
VAR visited_campvillain = false
VAR visited_castle = false

-> Tower


==== Tower ====
{ not visited_tower:
The tower is crooked, as if a large pair of hands gripped it at the base and midsection and gave it a twist.
  ~ visited_tower = true
}
There is a camp to the east of <strong><em>The Tower of Shirinorin</em></strong> and an allÃ©e to Merriment Forest in the south.
  + Head east
        -> CampHero 
  + Enter the forest
        -> Forest 

==== CampHero ====
{ not visited_camphero:
The camp is deserted. Horns of mead litter the ground, their last drops seeping into the soil. Rusty scraps of armor and shreds of bright colored clothing have been carelessly discarded. A lone guard blocks the entrance to the barracks.
  ~ visited_camphero = true
}
There are mountains in the east. The town of Overdown is just over the hill to the south of <strong><em>The Hero Camp</em></strong>. The warped Tower of Shirinorin remains in the west.
  + Head east
        -> Mountains
  + Go to town
        -> Town 
  + Go west
        -> Tower 
  + {not talked_to_hero_guard} Talk to the guard
        -> Action_Talk_HeroCamp_Guard

= Action_Talk_HeroCamp_Guard
The guard glares menacingly and refuses to talk.
  ~ talked_to_hero_guard = true
    -> CampHero

==== Mountains ====
{ not visited_mountains:
The majestic <strong><em>Flatbush Mountains</em></strong> sprawl before Balltholemew. The subterranean mines beyond the collapsed entrance are likely to be generated by a procedure.
  ~ visited_mountains = true
}
{ not ring_taken:
    A -4 Ring of Power glints in the haze near the foot of the nearest mountain. It seems unbelievably simple to be able to just walk in and pick it up like this, but here we are.
}
A village to the south is populated by goblins and orcs who have made it painstakingly clear that Balltholemew is not welcome there. There is a camp directly west.
  + Go west
        -> CampHero
  + {not ring_taken} Take the precious
        -> Action_Take_Precious

= Action_Take_Precious
This seems like an extremely bad idea so let's do it.
  ~ ring_taken = true
    -> Mountains

==== Forest ====
{ not visited_forest:
It is very dark. The forest is likely to be generated by a procedure.
  ~ visited_forest = true
}
The town of Overdown is directly east of <strong><em>Merriment Forest</em></strong>. The shores of Lake Inferior lie to the south. The twisted Tower of Shirinorin is due north.
  + Go north
        -> Tower
  + Head east
        -> Town 
  + Hop in the lake
        -> Lake

==== Lake ====
{ not visited_lake:
Balltholemew can't swim and all the available boats have been scattered along the shore. None of them are seaworthy.
  ~ visited_lake = true
}
Merriment Forest is up north and there is a camp to the east of <strong><em>Lake Inferior</em></strong>.
  + Back to the forest
        -> Forest
  + Investigate the camp
        -> CampVillain

==== CampVillain ====
{ not visited_campvillain:
The camp is surrounded by a purple aura that Balltholemew may neither pass nor see beyond.
  ~ visited_campvillain = true
}
The shores of Lake Inferior lie to the west of <strong><em>The Villain Camp</em></strong>. The town of Overdown is up the valley in the north. The ruins of a castle can be found due east.
  + Return to the lake
        -> Lake
  + Explore the castle
        -> Castle
  + Go to town
        -> Town 

==== Castle ====
{ not visited_castle:
The ruins of the once great <strong><em>Elsinore Castle</em></strong> stand before Balltholemew. Unlike the rest of the surrounding area, the castle was destroyed long ago. A dungeon below the castle is likely to be generated by a procedure.
  ~ visited_castle = true
}
{ not helm_taken:
    A +2 Helm of Water Walking dangles from a hook on the remnants of the outer gate.
}
A village to the north of <em><strong>Elsinore Castle</strong></em> is populated by goblins and orcs who have made it painstakingly clear that Balltholemew is not welcome there. There is a camp directly west.
  + Go west
        -> CampVillain
  + {not helm_taken} Take the helm
        -> Action_Take_Helm

= Action_Take_Helm
Balltholemew picks up the helm but, being an anthropomorphic crystal ball, can not walk on water or otherwise and has no use for it.
  ~ helm_taken = true
    -> Castle

==== Town ====
{ not visited_town:
The smithy, mill, market, tavern, and assorted dwellings lay in ruin. Piles of greenish soot smolder in the edges and corners of the alleyways.
  ~ visited_town = true
}
Balltholemew's friends are gathered in the <em><strong>Overdown</strong></em> town square. The Hero is camped in the north and the Villain is in the south. Merriment Forest is to the west.
  + Head south
        -> CampVillain
  + Go west
        -> Forest
  + Head north
        -> CampHero
  + Talk to friends
        -> TownSquare

= TownSquare
Kim Fischer, Dee Minor, and Jan Barderton stand in the middle of the square, visibly shaken.
  + {not talked_to_fisher} Talk to Fischer
        -> Action_Talk_Fisher
  + {not talked_to_miner} Talk to Minor
        -> Action_Talk_Miner 
  + {not talked_to_bard} Talk to Barderton
        -> Action_Talk_Bard
  + Freak the complete eff out
        -> Action_FreakOut 
  + Leave town
        -> Town
  + {talked_to_fisher} {talked_to_miner} {talked_to_bard} Find someone who will perform a quest in exchange for an item
        -> Act2 

= Action_Thank
Balltholemew thanks the villager kindly.
    -> TownSquare

= Action_Talk_Fisher
"Balltholemew! What a relief to see you! You just missed an epic struggle between the forces of good and evil and now our whole town is destroyed. I can't do my basic thing that makes me happy, which is climbing into a boat and catching fish, because all the ships are wrecked now. One of the heroes dropped this +3 Short Sword. I don't know much about melee combat, but perhaps you could find a use for it? Here, put it in your.. er.. um.. wherever it is that you keep things."
  ~ talked_to_fisher = true
  + Take the sword
        -> Action_Take_Sword
  + Politely decline
        -> Action_Decline_Sword

= Action_Take_Sword
Balltholemew puts the sword in.. um.. eh.. Balltholemew picks up the sword at any rate.
  ~ sword_taken = true
    -> Action_Thank

= Action_Decline_Sword
Balltholemew temporarily blinks out of existence and says "I can't really think of a use for this sword."
    -> Action_Thank

= Action_Talk_Miner
"Balltholemew! We're in the soup now! Once the shock wears off, I suppose I'll start constructing metal fortifications using the forge, once we can get it repaired. Hopefully by the time the Hero and Villain are ready to square off again, our impenetrable defenses will lead them to take their quarrel elsewhere. In the mean time, I happened to be holding this +1 Engineer's Hammer when our town exploded in a ball of green flame a moment ago. I can only use regular hammers and the smithy is in pretty rough shape, too rough to be used for storing enchanted hammers. Perhaps you can find a good home for it?"
  ~ talked_to_miner = true
  + Take the hammer
        -> Action_Take_Hammer
  + Politely decline
        -> Action_Decline_Hammer

= Action_Take_Hammer
Balltholemew takes the hammer.
  ~ hammer_taken = true
    -> Action_Thank

= Action_Decline_Hammer
Balltholemew temporarily blinks out of existence and says "I can't really think of a use for this hammer either."
    -> Action_Thank

= Action_Talk_Bard
"Balltholemew! I don't have much time as I need to compose an epic saga about the battle that happened like 5 minutes ago and set it to song immediately. Unfortunately my lyre strings are all broken so I doubt I'll get very far. You could do me a solid tho and take this -2 Leprechaun Flute off my hands. It's been nothing but trouble!"
  ~ talked_to_bard = true
  + Take the flute
        -> Action_Take_Flute
  + Politely decline
        -> Action_Decline_Flute 

= Action_Take_Flute
Balltholemew takes the flute.
  ~ flute_taken = true
    -> Action_Thank

= Action_Decline_Flute 
Balltholemew temporarily blinks out of existence and says "I can't really think of a use for this flute either."
    -> Action_Thank

= Action_FreakOut
Balltholemew explodes into a mist. The particles begin to coalesce, first into droplets, then smaller balls which fall to the ground. They converge on a central point and Balltholemew is whole again.
    -> TownSquare

==== Act2 ====
The game begins in earnest. The Player (Balltholemew, Fischer, Minor, or Barderton) must collect enchanted or cursed items, which will be attractive to either the Hero or the Villain respectively. These items can be given as rewards for performing various quests which may simply keep the Hero and/or Villain occupied, buying time for the town to be repaired and fortified.
The Player is unable to equip the items directly and may only use them to assign quests. The Hero or Villain performing a quest may cause environmental effects like landslides and flooding. They may cause the next battle to be staged somewhere other than Overdown.
There may be other class-specific ways to tip the scales, but beware! If the Villain is ready for war and the Hero isn't, they will simply take over the town. Likewise, if the Hero regains strength while the Villain is still vulnerable, they will quickly dispatch the Villain and throw a wild party in the tavern which will be three times as destructive as the actual battle. The only ways to win are to successfully fortify the town before the next battle, or ensure it happens far, far away.
    -> END`;
